pluginManagement {
    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        google()
        mavenCentral()
        maven {
            name = "GithubPackagesRepo"
            url = "https://maven.pkg.github.com/liu-wanshun/platform_frameworks_base"
            credentials {
                /*
                Github Action 中使用 System.getenv()
                本地在gradle.properties中声明"gpr.user"和"gpr.key",详见:
                1:https://docs.gradle.org/current/userguide/build_environment.html#sec:gradle_configuration_properties
                2:https://docs.github.com/en/packages/working-with-a-github-packages-registry/working-with-the-gradle-registry#using-a-published-package
                **/
                username = providers.gradleProperty("gpr.user").orNull ?: System.getenv("USERNAME")
                password = providers.gradleProperty("gpr.key").orNull ?: System.getenv("GITHUB_TOKEN")
            }
        }
        maven { url "https://jitpack.io" }
        maven { url "https://maven.aliyun.com/repository/public" }
    }
}
rootProject.name = "LauncherC"
include ':iconloaderlib'
project(':iconloaderlib').projectDir = new File(rootDir, 'platform_frameworks_libs_systemui/iconloaderlib')

include ':searchuilib'
project(':searchuilib').projectDir = new File(rootDir, 'platform_frameworks_libs_systemui/searchuilib')

include ':viewcapturelib'
project(':viewcapturelib').projectDir = new File(rootDir, 'platform_frameworks_libs_systemui/viewcapturelib')

final def PLATFORM_FRAMEWORKS_BASE_PATH = "platform_frameworks_base"
// 加载本地 module, https://github.com/liu-wanshun/platform_frameworks_base
if (file("${PLATFORM_FRAMEWORKS_BASE_PATH}/packages/SystemUI/shared").exists()) {
    // SystemUI shared dependencies
    include ':wm_shell'
    project(':wm_shell').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/libs/WindowManager/Shell")
    include ':SystemUIAnimation'
    project(':SystemUIAnimation').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/packages/SystemUI/animation")
    include ':SystemUIPlugin'
    project(':SystemUIPlugin').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/packages/SystemUI/plugin")
    include ':PluginCore'
    project(':PluginCore').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/packages/SystemUI/plugin_core")
    include ':SystemUIShared'
    project(':SystemUIShared').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/packages/SystemUI/shared")
    include ':SystemUIUnfold'
    project(':SystemUIUnfold').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/packages/SystemUI/unfold")
    // quickstep compat
    include ':quickstepcompat'
    project(':quickstepcompat').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/quickstepcompat")
    include ':quickstepcompatQ'
    project(':quickstepcompatQ').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/quickstepcompat/quickstepcompatQ")
    include ':quickstepcompatR'
    project(':quickstepcompatR').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/quickstepcompat/quickstepcompatR")
    include ':quickstepcompatS'
    project(':quickstepcompatS').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/quickstepcompat/quickstepcompatS")
    include ':quickstepcompatT'
    project(':quickstepcompatT').projectDir = new File("${PLATFORM_FRAMEWORKS_BASE_PATH}/quickstepcompat/quickstepcompatT")
}
include ':recents_rro'
